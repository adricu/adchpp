Import('dev source_path')

targets = []

def runCommand(cmd):
	import os
	try:
		i, o = os.popen4(cmd)
		i.close()
		output = o.read()
		o.close()
		return output
	except Exception,e:
		return ""

def getRbConf(name):
	if dev.env['PLATFORM'] == 'win32':
		return runCommand("ruby -rmkmf -e \"print Config.expand('$(" + name + ")')\"")
	else:
		return runCommand("ruby -rmkmf -e \"print Config.expand('\$(" + name + ")')\"")

def buildRbModule():
	if not runCommand("ruby -v"):
		print "Ruby not found"
		return
	
	env = dev.env.Copy()
	
	env.Append(SWIGFLAGS=['-c++','-Wall','-ruby'])
	f = env.CFile(target=dev.get_target(source_path, 'ruby_wrap.cxx'), source='ruby.i')
	
	import distutils.sysconfig
	
	libdir = getRbConf("libdir")
	if not libdir:
		print "Ruby libdir not found"
		return 
	
	incdir = getRbConf("archdir")
	if not incdir:
		print "Ruby archdir not found"
		return

	soname = getRbConf("RUBY_SO_NAME")
	if not soname:
		print "Ruby RUBY_SO_NAME not found"
		return
	
	if '_DEBUG' in env['CPPDEFINES']:
		env['CPPDEFINES'].remove('_DEBUG')
	
	env.Append(LIBPATH=[libdir])
	env.Append(CPPPATH=['#', incdir])
	env.Append(LIBS=[soname, 'adchpp'])
		
	rb = env.SharedLibrary(dev.get_target(source_path, 'rbadchpp'), [f],
						SHLIBPREFIX='')
	targets.append(rb)


def buildPyModule():
	env = dev.env.Copy()
	
	env.Append(SWIGFLAGS=['-c++','-threads','-Wall','-python'])
	f = env.CFile(target=dev.get_target(source_path, 'python_wrap.cxx'), source='python.i')
	
	env.SideEffect(dev.get_target(source_path, 'pyadchpp.py'), f)
	
	import distutils.sysconfig
	
	env.Append(CPPPATH=['#', distutils.sysconfig.get_python_inc()])
	if '_DEBUG' in env['CPPDEFINES']:
		env['CPPDEFINES'].remove('_DEBUG')
	if '/MDd' in env['CCFLAGS']:
		env['CCFLAGS'].remove('/MDd')
		env['CCFLAGS'].append('/MD')
		
	if env['PLATFORM'] == 'win32':
		env.Append(LIBPATH=['c:/python24/libs'])
		env.Append(LIBS=['python24', 'adchpp'])
	else:
		env.Append(LIBS=['python2.4', 'adchpp'])
		
	pyd = env.SharedLibrary(dev.get_target(source_path, '_pyadchpp'), [f],
						SHLIBPREFIX='')
	targets.append(pyd)

# buildRbModule() - needs threading sorted out
buildPyModule()
Return('targets')
