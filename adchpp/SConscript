# vim: set filetype=py

def getRevision(env):
	try:
		import bzrlib
		import bzrlib.builtins

		tree = bzrlib.builtins.WorkingTree.open_containing(env.Dir("#").abspath)[0]

		return tree.branch.revision_id_to_revno(tree.last_revision())
	except ImportError:
		return 0
 
Import('dev source_path')

env, target, sources = dev.prepare_build(source_path, 'adchpp', shared_precompiled_header = 'adchpp')

env.Append(CPPPATH = ['.'])

env.Append(CPPDEFINES=["BUILDING_ADCHPP=1"])

if env['CC'] == 'cl': # MSVC
	env.Append(LIBS = ['advapi32', 'user32'])

if 'HAVE_DL' in env['CPPDEFINES']:
    env.Append(LIBS = ['dl'])

if 'HAVE_PTHREAD' in env['CPPDEFINES']:
    env.Append(LIBS = ['pthread'])

if 'HAVE_OPENSSL' in env['CPPDEFINES']:
	if dev.is_win32():
		if env['CC'] == 'cl': # MSVC
			if dev.mode == 'debug':
				env.Prepend(LIBS = ['ssleay32d', 'libeay32d'])
			else:
				env.Prepend(LIBS = ['ssleay32', 'libeay32'])
		else:
			env.Prepend(LIBS = ['ssl', 'crypto'])
		env.Append(LIBS = ['gdi32']) # something in OpenSSL uses CreateDC etc...
		env.Append(CPPPATH=['#/openssl/include'])
		openssl_lib = '#/openssl/lib/'
		if env['arch'] != 'x86':
			openssl_lib += env['arch'] + '/'
		env.Append(LIBPATH=[openssl_lib])
	else:
		env.Prepend(LIBS = ['ssl', 'crypto'])

for i, source in enumerate(sources):
	if source.find("version.cpp") != -1:
		rev = ['ADCHPP_REVISION=' + str(getRevision(env))]
		sources[i] = env.SharedObject(source, CPPDEFINES=env['CPPDEFINES'] + rev)

headers=dev.get_sources(source_path, "*.h")

ret = env.SharedLibrary(target, sources)

Return('ret')
