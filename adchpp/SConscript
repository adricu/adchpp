# vim: set filetype=py

def getRevision(env):
	try:
		import subprocess
		ret = subprocess.check_output('hg identify -i', shell=True)
		if ret:
			return ret[:-1]  # Remove the trailing line-ending char. @todo: This returns a 2 byte line-ending when compiled on Win so there's some cruft in the end of the generated revno string on Win builds...
	except:
		pass

	return '[unknown]'

 
Import('dev source_path')

env, target, sources = dev.prepare_build(source_path, 'adchpp', shared_precompiled_header = 'adchpp')

env.Append(CPPPATH = ['.'])

env.Append(CPPDEFINES=["BUILDING_ADCHPP=1"])

if env['CC'] == 'cl': # MSVC
	env.Append(LIBS = ['advapi32', 'user32'])

if 'HAVE_DL' in env['CPPDEFINES']:
    env.Append(LIBS = ['dl'])

if 'HAVE_PTHREAD' in env['CPPDEFINES']:
    env.Append(LIBS = ['pthread'])

if 'HAVE_OPENSSL' in env['CPPDEFINES']:
	if dev.is_win32():
		if env['CC'] == 'cl': # MSVC
			if env['mode'] == 'debug':
				env.Prepend(LIBS = ['ssleay32d', 'libeay32d'])
			else:
				env.Prepend(LIBS = ['ssleay32', 'libeay32'])
		else:
			env.Prepend(LIBS = ['ssl', 'crypto'])
		env.Append(LIBS = ['gdi32']) # something in OpenSSL uses CreateDC etc...
		env.Append(CPPPATH=['#/openssl/include'])
		openssl_lib = '#/openssl/lib/'
		if env['arch'] != 'x86':
			openssl_lib += env['arch'] + '/'
		env.Append(LIBPATH=[openssl_lib])
	else:
		env.Prepend(LIBS = ['ssl', 'crypto'])

for i, source in enumerate(sources):
	if source.find("version.cpp") != -1:
		rev = ['ADCHPP_REVISION=' + getRevision(env)]
		sources[i] = env.SharedObject(source, CPPDEFINES=env['CPPDEFINES'] + rev)

headers=dev.get_sources(source_path, "*.h")

ret = env.SharedLibrary(target, sources)

Return('ret')
